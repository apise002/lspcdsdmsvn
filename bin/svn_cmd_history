#! /bin/csh -f

set lib_dir  = `echo $argv[1] | awk -F\= '{print $2}'`
set lib      = `echo $argv[2] | awk -F\= '{print $2}'`
set cell     = `echo $argv[3] | awk -F\= '{print $2}'`
set view     = `echo $argv[4] | awk -F\= '{print $2}'`
set cat      = `echo $argv[5] | awk -F\= '{print $2}'`

###########################################################################################################
set lib_dir      = `echo $lib_dir | sed -r 's#/+#/#g' | sed 's#/$##'`
set timetag      = `date +%Y%m%d%H%M%S`
set tmp_file_log = /tmp/${user}_lspcdsdmsvn_log_${timetag}
set tmp_file_st  = /tmp/${user}_lspcdsdmsvn_st_${timetag}
###########################################################################################################


###########################################################################################################
if !($?LSPCDSDMSVN_HOME) then
    echo "ERROR : Please source lspcdsdmsvn.cshrc firstly." >&! $tmp_file_log
	goto Report
endif
###########################################################################################################

###########################################################################################################
###########################################################################################################
echo "[INFOR] SVN History (only for OA file) ..." >&! $tmp_file_log
if (($lib != "") && ($cell != "") && ($view != "")) then
    ####view is folder/file
    set svn_cmd_dir = $lib_dir/$lib/$cell/$view
	set display_dir = $lib/$cell/$view
else
    echo "ERROR : args case is not supported" >>&! $tmp_file_log
	echo "      : args =lib=cell=view=cat= : =$lib=$cell=$view=$cat=" >>&! $tmp_file_log
	goto Report
endif


###########################################################################################################
###########################################################################################################
echo $svn_cmd_dir | xargs -n 1 | xargs -i echo "    path => {}" >>&! $tmp_file_log
echo "" >>&! $tmp_file_log

set context = ""
set OAfile = (`find $svn_cmd_dir -maxdepth 1 -name "*.oa" -type f`)
if ($#OAfile == 0) then
    echo "ERROR : $display_dir has no oa file , may be the empty folder ." >>&! $tmp_file_log
	goto Report
else if ($#OAfile != 1) then
    echo "ERROR : $display_dir has more then one oa file , svn history not supported ." >>&! $tmp_file_log
	goto Report
else
    svn log $OAfile >&! $tmp_file_st
    if (($status != 0) || (`cat $tmp_file_st | grep -Ec "\sW[0-9]+:\s|\sE[0-9]+:\s"`)) then
        cat $tmp_file_st >>&! $tmp_file_log
    	echo "  ==> Above ERROR at step 1" >>&! $tmp_file_log
        goto Report
    endif
    
    foreach line (`cat $tmp_file_st | grep -E "^r[0-9]+\s\|" | sed "s/\s/@@@@/g"`)
        set line  = `echo $line | sed "s/@@@@/ /g" | sed "s/|//g"`
    	set ver   = `echo $line | sed -r "s/(\S+)\s+(\S+)\s+(\S+\s+\S+)\s+.*/\1/"`
    	set owner = `echo $line | sed -r "s/(\S+)\s+(\S+)\s+(\S+\s+\S+)\s+.*/\2/"`
    	set time  = `echo $line | sed -r "s/(\S+)\s+(\S+)\s+(\S+\s+\S+)\s+.*/\3/"`
    	set context = ($context $ver $owner \"$time\")
    end
    set cmd = 'zenity --list --title=lspCDSDMSVN_History --text "Cell : $display_dir" --width=800 --height=400 --separator=" " --column=version --column=owner --column=time '"$context"
    eval $cmd
endif

####for skill callback
echo "[INFOR] Get SVN History succeeful." >>&! $tmp_file_log
echo "succeed"
goto FINAL


###########################################################################################################
###########################################################################################################
Report:
echo "" >>&! $tmp_file_log
echo "[INFOR] any problem or question, contact apise_001@163.com" >>&! $tmp_file_log
zenity --title=lspCDSDMSVN_TAG --width=1200 --height=400 --text-info --filename=$tmp_file_log

FINAL:
\rm -rf $tmp_file_log
\rm -rf $tmp_file_st